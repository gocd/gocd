/*
 * Copyright 2024 Thoughtworks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


import com.thoughtworks.go.build.*
import org.apache.commons.codec.digest.DigestUtils

private File destFile(String url) {
  new File(gradle.gradleUserHomeDir, "download-cache/${DigestUtils.md5Hex(url)}/${new File(new URL(url).path).name}")
}

task downloadMacJreX64Checksum(type: DownloadFile) {
  src project.packagedJavaVersion.toSha256SumURLFor(OperatingSystem.mac, Architecture.x64)
  dest destFile(src.toString())
}

task downloadMacJreX64(type: DownloadFile) {
  dependsOn downloadMacJreX64Checksum
  src project.packagedJavaVersion.toDownloadURLFor(OperatingSystem.mac, Architecture.x64)
  dest destFile(src.toString())
  checksum = { downloadMacJreX64Checksum.outputs.files.singleFile.getText("utf-8").trim().split(" ").first() }
}

task downloadMacJreArm64Checksum(type: DownloadFile) {
  src project.packagedJavaVersion.toSha256SumURLFor(OperatingSystem.mac, Architecture.aarch64)
  dest destFile(src.toString())
}

task downloadMacJreArm64(type: DownloadFile) {
  dependsOn downloadMacJreArm64Checksum
  src project.packagedJavaVersion.toDownloadURLFor(OperatingSystem.mac, Architecture.aarch64)
  dest destFile(src.toString())
  checksum = { downloadMacJreArm64Checksum.outputs.files.singleFile.getText("utf-8").trim().split(" ").first() }
}

def configureMacZip(Zip zipTask, InstallerType installerType, Zip genericZipTask, Architecture arch) {
  DownloadFile downloadTask = arch == Architecture.aarch64 ? downloadMacJreArm64 : downloadMacJreX64

  zipTask.with {
    group project.name
    description "Build the ${installerType.baseName} osx (zip) installer"

    dependsOn downloadTask
    dependsOn genericZipTask
    destinationDirectory = file("${project.buildDir.path}/${project.base.distsDirName}/osx")
    archiveBaseName = installerType.baseName
    archiveClassifier = arch == Architecture.aarch64 ? 'osx-aarch64' : 'osx'

    def genericZipTree = zipTree(genericZipTask.archiveFile)

    // dont include the wrapper.conf, and tanuki wrappers for OSes other than osx
    from(genericZipTree) {
      exclude "${installerType.baseName}-${project.goVersion}/wrapper-config/wrapper.conf"
      exclude "${installerType.baseName}-${project.goVersion}/wrapper/wrapper-*"
      exclude "${installerType.baseName}-${project.goVersion}/wrapper/libwrapper-*"
      exclude "${installerType.baseName}-${project.goVersion}/bin/*.bat"
    }

    def wrapperArch = arch == Architecture.aarch64 ? 'arm-64' : 'universal-64'

    from(genericZipTree) {
      include "${installerType.baseName}-${project.goVersion}/wrapper/wrapper-macosx-${wrapperArch}"
      include "${installerType.baseName}-${project.goVersion}/wrapper/libwrapper-macosx-${wrapperArch}*"
    }

    // include the wrapper.conf, but replace the java command
    from(genericZipTree) {
      include "${installerType.baseName}-${project.goVersion}/wrapper-config/wrapper.conf"
      filter { String eachLine ->
        if (eachLine == 'wrapper.java.command=java') {
          eachLine = 'wrapper.java.command=jre/Contents/Home/bin/java'
        }
        eachLine
      }
    }

    // puts the content of the tar under `go-{agent,server}-XXX/jre`
    from(tarTree(downloadTask.outputs.files.singleFile)) {
      eachFile { FileCopyDetails fcd ->
        List<String> segments = fcd.relativePath.segments
        segments.set(1, "jre")
        fcd.relativePath = new RelativePath(!fcd.isDirectory(), (segments as String[]))
      }
      into("${installerType.baseName}-${project.goVersion}")
    }
  }

  zipTask.finalizedBy(project.tasks.create("${zipTask.name}Metadata", InstallerMetadataTask.class) {
    architecture = arch
    adoptiumVersion = project.packagedJavaVersion
    packageTask = zipTask
    type = installerType
  })
}

task agentMacX64Zip(type: Zip) { Zip thisTask ->
  configureMacZip(thisTask, InstallerType.agent, agentGenericZip, Architecture.x64)
}

task serverMacX64Zip(type: Zip) { Zip thisTask ->
  configureMacZip(thisTask, InstallerType.server, serverGenericZip, Architecture.x64)
}

task agentMacArm64Zip(type: Zip) { Zip thisTask ->
  configureMacZip(thisTask, InstallerType.agent, agentGenericZip, Architecture.aarch64)
}

task serverMacArm64Zip(type: Zip) { Zip thisTask ->
  configureMacZip(thisTask, InstallerType.server, serverGenericZip, Architecture.aarch64)
}

assemble.dependsOn(":installers:agentMacX64Zip", ":installers:serverMacX64Zip", ":installers:agentMacArm64Zip", ":installers:serverMacArm64Zip")
