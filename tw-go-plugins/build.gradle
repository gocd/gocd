/*
 * Copyright Thoughtworks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import com.thoughtworks.go.build.*

configurations {
  pluginsZipConfig
}

def dependencies = [
  new GithubArtifact(
    user: 'gocd',
    repo: 'gocd-ldap-authentication-plugin',
    tagName: 'v3.0.0-509',
    asset: 'gocd-ldap-authentication-plugin-3.0.0-509.jar',
    checksum: '103a51446885b8206bbd145948fd9da354760c75aa4890499ecdd2d9b1125fb1'
  ),
  new GithubArtifact(
    user: 'gocd',
    repo: 'gocd-filebased-authentication-plugin',
    tagName: 'v2.2.0-401',
    asset: 'gocd-filebased-authentication-plugin-2.2.0-401.jar',
    checksum: 'cf4810a6ac5e1c2a69a8e68b7002b054ddfd45183531bf05e33749cc6bc3d70f'
  ),
  new GithubArtifact(
    user: 'tomzo',
    repo: 'gocd-yaml-config-plugin',
    tagName: 'v1.0.0-524',
    asset: 'yaml-config-plugin-1.0.0-524.jar',
    checksum: '964b6afa740bb3600dada09250dfe3c4e95d29211615769613651603cc82bc39'
  ),
  new GithubArtifact(
    user: 'tomzo',
    repo: 'gocd-json-config-plugin',
    tagName: 'v1.0.0-370',
    asset: 'json-config-plugin-1.0.0-370.jar',
    checksum: '79ff2aac3fc04aa686a826b77f2e98ea5294e55df253179d59ac77dbdbd6f7e6'
  ),
  new GithubArtifact(
    user: 'gocd',
    repo: 'gocd-file-based-secrets-plugin',
    tagName: 'v1.2.1-420',
    asset: 'gocd-file-based-secrets-plugin-1.2.1-420.jar',
    checksum: '985a9b7640df6c66a496b455d30c8e43252da17f06a51cd5dd50d769b326ceb5'
  )
]

tasks.register('createVersionFile') {
  def goVersions = project.goVersions as GoVersions
  inputs.property('goVersions', goVersions)

  def file = layout.buildDirectory.file("version.txt").get().asFile
  outputs.file file

  doLast {
    file.getParentFile().mkdirs()
    file.withWriter { out ->
      out.write("${goVersions.fullVersion} (${goVersions.gitRevision})")
    }
  }
}

def downloadPlugins = tasks.register('downloadPlugins')

def pluginsZip = tasks.register('pluginsZip', Zip) {
  finalizedBy 'verifyPluginZip'

  archiveBaseName = 'plugins'
  dependsOn createVersionFile
  from(createVersionFile)

  from(dependencies.collect { dep -> destFile(dep) })
}

tasks.register('verifyPluginZip', VerifyJarTask) {
  jarTask = pluginsZip
  expectedJars = [
    '/': dependencies.collect { dep -> "${dep.repo}.jar".toString() }
  ]
}

artifacts {
  pluginsZipConfig pluginsZip
}

dependencies.each { dep ->
  def downloadTask = tasks.register("download-${dep.repo}", DownloadFile) { downloadTask ->
    downloadTask.src dep.downloadUrl
    downloadTask.dest destFile(dep)
    downloadTask.checksum = dep.checksum
  }

  downloadPlugins.configure {
    dependsOn downloadTask
  }

  pluginsZip.configure { Zip zipTask ->
    zipTask.dependsOn downloadTask
    zipTask.from(destFile(dep))
  }
}

private File destFile(GithubArtifact dep) {
  new File(gradle.gradleUserHomeDir, "download-cache/${TextUtils.md5Hex(dep.downloadUrl)}/${dep.repo}.jar")
}

tasks.register('prepare') { dependsOn pluginsZip }
