/*
 * Copyright 2020 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import com.thoughtworks.go.build.VerifyJarTask

description = 'GoCD TFS SDK wrapper v14'

dependencies {
  compile project(':common')
  compile group: 'com.microsoft', name: 'tfs-sdk', version: '14.118.0'
  // not a compile dependency, but some classloader hell
  compile(group: 'xalan', name: 'xalan', version: '2.7.1')
  testCompileOnly group: 'junit', name: 'junit', version: project.versions.junit
  testRuntimeOnly group: 'org.junit.vintage', name: 'junit-vintage-engine', version: project.versions.junit5
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: project.versions.junit5
  testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: project.versions.junit5
  testCompile group: 'org.hamcrest', name: 'hamcrest-core', version: project.versions.hamcrest
  testCompile group: 'org.mockito', name: 'mockito-core', version: project.versions.mockito
  testCompile group: 'org.assertj', name: 'assertj-core', version: project.versions.assertJ
}

jar {
  archiveClassifier = 'classes'
}

task fatJar(type: Jar) {
  finalizedBy 'verifyJar'
  dependsOn jar
  from(jar.archivePath) { into('lib/') }

  from(project.configurations.compile.findAll {
    it.absolutePath.contains('tfs-sdk') || it.absolutePath.contains('xalan') || it.absolutePath.contains('serializer')
  }) {
    into 'lib/'
  }

  from("${project.projectDir}/tfssdk/native") {
    include "**/*.*"
    into "tfssdk/native"
  }
}

task verifyJar(type: VerifyJarTask) {
  jarTask = fatJar
  expectedJars = [
      "lib": [
          "tfs-impl-14-${project.version}-classes.jar",
          "tfs-sdk-14.118.0.jar"
      ]
  ]
}

artifacts {
  fatJarConfig fatJar
}
