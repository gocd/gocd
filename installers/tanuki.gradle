import com.thoughtworks.go.build.DownloadFile
import org.apache.commons.codec.digest.DigestUtils

/*
 * Copyright 2022 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

private File destFile(String url) {
  new File(gradle.gradleUserHomeDir, "download-cache/${DigestUtils.md5Hex(url)}/${new File(new URL(url).path).name}")
}

task downloadTanukiDeltaPack(type: DownloadFile) {
  def srcUrl = System.getenv("TANUKI_WRAPPER_URL") ?: "https://nexus.gocd.io/repository/s3-mirrors/local/tanuki/wrapper-delta-pack-${project.versions.tanuki}-st.tar.gz"
  src srcUrl
  dest destFile(srcUrl)
  checksum '27b1d3da6982bcdf1cc51f3bb99abb8763635ae4691e3fe36e310c818b3bbb42'
}

task extractDeltaPack(type: Copy) {
  dependsOn downloadTanukiDeltaPack
  from(project.tarTree(downloadTanukiDeltaPack.outputs.files.singleFile))
  into project.buildDir
}
