import com.thoughtworks.go.build.VerifyJarTask

/*
 * Copyright 2016 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

description = 'GoCD TFS SDK wrapper'

dependencies {
  compile project(':common')
  compile group: 'com.microsoft', name: 'tfs-sdk', version: '10.1.0'
  compile(group: 'xalan', name: 'xalan', version: '2.7.1') {
    exclude(module: 'xercesImpl')
  }
  testCompile group: 'junit', name: 'junit', version: '4.12'
  testCompile group: 'com.googlecode', name: 'junit-ext', version: '1.0'
  testCompile group: 'org.hamcrest', name: 'hamcrest-core', version: '1.3'
  testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
  testCompile group: 'org.mockito', name: 'mockito-core', version: '1.9.5'
}

jar {
  classifier 'classes'
}

task fatJar(type: Jar, dependsOn: [jar]) {

  doFirst {
    from(jar.archivePath) { into('lib/') }

    def deps = project.configurations.compile.findAll {
      it.absolutePath.contains('tfs-sdk') || it.absolutePath.contains('xalan') || it.absolutePath.contains('serializer')
    }

    from(deps) {
      into 'lib/'
    }

    from("${project.projectDir}/tfssdk/native") {
      include "**/*.*"
      into "tfssdk/native"
    }
  }

  doLast {
    verifyJar.execute()
  }
}

task verifyJar(type: VerifyJarTask) {
  jarTask = fatJar
  expectedJars = [
      "lib": [
          "serializer-2.7.1.jar",
          "tfs-impl-${project.version}-classes.jar",
          "tfs-sdk-10.1.0.jar",
          "xalan-2.7.1.jar",
      ]
  ]
}

artifacts {
  fatJarConfig fatJar
}
