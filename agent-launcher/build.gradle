/*
 * Copyright 2024 Thoughtworks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import com.thoughtworks.go.build.VerifyJarTask

description = 'Agent Launcher Jar'

configurations {
  testRuntimeClasspath.extendsFrom(packagingOnly)
}

dependencies {
  packagingOnly project.deps.bouncyCastle
  packagingOnly project.deps.bouncyCastlePkix

  implementation project(path: ':agent-common', configuration: 'runtimeElements')

  testImplementation project(path: ':test:test-utils', configuration: 'runtimeElements')
  testImplementation project.deps.junit5Api
  testImplementation project.deps.mockitoCore
  testRuntimeOnly project.deps.junit5Engine
}

jar {
  archiveClassifier = 'classes'
}

task fatJar(type: Jar) {
  finalizedBy 'verifyJar'
  dependsOn jar, configurations.packagingOnly, configurations.runtimeClasspath

  manifest {
    attributes 'Go-Agent-Launcher-Class': 'com.thoughtworks.go.agent.launcher.AgentLauncherImpl'
    attributes 'Go-Agent-Launcher-Lib-Dir': 'libs'
  }

  from(configurations.runtimeClasspath + configurations.packagingOnly + jar.archiveFile) { into "libs/" }
}

task verifyJar(type: VerifyJarTask) {
  jarTask = fatJar
  expectedJars = [
    "libs": [
      "agent-common-${project.version}.jar",
      "agent-launcher-${project.version}-classes.jar",
      "base-${project.version}.jar",
      "bcpkix-jdk18on-${project.versions.bouncyCastle}.jar",
      "bcprov-jdk18on-${project.versions.bouncyCastle}.jar",
      "bcutil-jdk18on-${project.versions.bouncyCastle}.jar",
      "commons-io-${project.versions.commonsIO}.jar",
      "commons-lang3-${project.versions.commonsLang3}.jar",
      "httpclient-${project.versions.apacheHttpComponents}.jar",
      "httpcore-4.4.16.jar",
      "jcl-over-slf4j-${project.versions.slf4jBom}.jar",
      "jcommander-${project.versions.jcommander}.jar",
      "joda-time-${project.versions.jodaTime}.jar",
      "logback-classic-${project.versions.logback}.jar",
      "logback-core-${project.versions.logback}.jar",
      "slf4j-api-${project.versions.slf4jBom}.jar",
    ]
  ]
}

artifacts {
  fatJarConfig fatJar
}
