/*
 * Copyright 2021 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

description = 'GoCD Test Utilities'

configurations {
  copyOnly {
    transitive = false
  }
}

dependencies {
  implementation project(':base')
  api project(':util')
  api project.deps.jsontools
  api project.deps.springTest
  implementation project.deps.jetty
  implementation project.deps.jettyPlus
  implementation project.deps.jettyJmx
  implementation project.deps.jettyUtil
  implementation project.deps.jettyServlet
  api project.deps.jettyWebapp
  implementation project.deps.mail
  api project.deps.systemRules

  api project.deps.assertJ
  compileOnly project.deps.junit4
  runtimeOnly project.deps.junit5Vintage
  api project.deps.junit5
  runtimeOnly project.deps.junit5Engine
  api project.deps.junit5Params

  api files('vendored/com.googlecode.junit-ext-1.0.jar')
  api project.deps.hamcrest
  api project.deps.hamcrestLibrary

  copyOnly project(path: ':agent-launcher', configuration: 'fatJarConfig')
  copyOnly project(path: ':test:test-agent')
  copyOnly project(path: ':tfs-impl:tfs-impl-14', configuration: 'fatJarConfig')
}

def generatedResourcesOutput = file("resources-generated")

sourceSets {
  main {
    resources {
      output.dir generatedResourcesOutput, builtBy: 'generateResources'
      srcDirs += generatedResourcesOutput
    }
  }
}

clean {
  delete(generatedResourcesOutput)
}

task generateResources(type: Copy) {
  into "$generatedResourcesOutput/testdata/gen"
  from(project.configurations.copyOnly) {
    rename "(.*)-${project.version}.(jar|zip)", '$1.$2'
  }
}

task prepare {
  dependsOn generateResources
}

