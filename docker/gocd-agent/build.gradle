import com.thoughtworks.go.build.docker.BuildDockerImageTask
import com.thoughtworks.go.build.docker.Distro
import com.thoughtworks.go.build.docker.DistroVersion
import com.thoughtworks.go.build.docker.ImageType
import groovy.json.JsonOutput

/*
 * Copyright 2021 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'base'

subprojects {
  apply plugin: 'base'
  project.buildDir = file("${parent.buildDir}/${project.name}")

  // detect the distro name and version from the project name.
  // For e.g. `centos-6` will map to `Distro.centos` and version `6` as returned by `Distro.centos.supportedVersions`
  def distroName = project.name.split('-').first()
  Distro distro = Distro.valueOf(distroName)

  if (!project.name.startsWith("${distro.name()}-")) {
    throw new GradleException("Did you initialize the project ${project.path} with the wrong distro(${distro.name()})?".toString())
  }

  def distroVersionOnProject = project.name.replaceFirst("${distro.name()}-", "")
  DistroVersion distroVersion = distro.getVersion(distroVersionOnProject)

  task docker(type: BuildDockerImageTask) { BuildDockerImageTask task ->
    assemble.dependsOn(task)

    if (project.hasProperty('dockerBuildLocalZip')) {
      task.dependsOn ':installers:agentGenericZip'
      task.artifactZip = rootProject.project(':installers').tasks.getByName('agentGenericZip').outputs.files.singleFile
    } else if (project.hasProperty('dockerbuildAgentZipLocation')) {
      task.artifactZip = project.file(project.dockerbuildAgentZipLocation)
    } else {
      task.doFirst {
        throw new GradleException("You must specify either -PdockerBuildLocalZip or -PdockerbuildAgentZipLocation=/path/to/agent.zip")
      }
    }

    task.description = "Generate the dockerfile for GoCD agent running on ${distro.name()} v${distroVersion.version}"
    task.group = BasePlugin.BUILD_GROUP

    task.distro = distro
    task.distroVersion = distroVersion
    task.tiniVersion = project.versions.tini
    task.imageType = ImageType.agent
    task.outputDir = project.file("${project.parent.buildDir}/docker")

    task.templateHelper = {
      project.delete(project.fileTree(task.gitRepoDirectory).include("*"))

      project.copy {
        from rootProject.file("LICENSE")
        into task.gitRepoDirectory
      }

      task.writeTemplateToFile("README.md.ftl", project.file("${task.gitRepoDirectory}/README.md"))

      [
        'agent-bootstrapper-logback-include.xml',
        'agent-launcher-logback-include.xml',
        'agent-logback-include.xml',
      ].forEach { eachFile ->
        project.file("${task.gitRepoDirectory}/${eachFile}").bytes = BuildDockerImageTask.class.getResource("/gocd-docker-agent/${eachFile}").bytes
      }

      [
        'docker-entrypoint.sh',
      ].forEach { eachFile ->
        def file = project.file("${task.gitRepoDirectory}/${eachFile}")

        file.bytes = BuildDockerImageTask.class.getResource("/gocd-docker-agent/${eachFile}").bytes
        file.setExecutable(true, false)
      }

      if (distro == Distro.docker) {
        def file = project.file("${task.gitRepoDirectory}/run-docker-daemon.sh")
        file.bytes = BuildDockerImageTask.class.getResource("/gocd-docker-agent/run-docker-daemon.sh").bytes
        file.setExecutable(true, false)

        file = project.file("${task.gitRepoDirectory}/dockerd-sudo")
        file.bytes = BuildDockerImageTask.class.getResource("/gocd-docker-agent/dockerd-sudo").bytes
      }
    }

    task.verifyHelper = {
      // test image
      // daemonize the container
      project.exec {
        workingDir = project.rootProject.projectDir
        commandLine = ["docker", "run", "--rm", "-e", "GO_SERVER_URL=http://localhost:8153/go", "-d", "--name", docker.dockerImageName, docker.imageNameWithTag]
        standardOutput = System.out
        errorOutput = System.err
      }

      Thread.sleep(5000)

      ByteArrayOutputStream baos = new ByteArrayOutputStream()

      // run a `ps aux`
      project.exec {
        workingDir = project.rootProject.projectDir
        commandLine = ["docker", "exec", docker.dockerImageName, "ps", "aux"]
        standardOutput = baos
        errorOutput = baos
      }

      // stop the container
      project.exec {
        workingDir = project.rootProject.projectDir
        commandLine = ["docker", "stop", docker.dockerImageName]
        standardOutput = System.out
        errorOutput = System.err
      }

      // assert if process was running
      def expectedString = "lib/agent-bootstrapper.jar -serverUrl http://localhost:8153/go"
      def processList = baos.toString()
      if (!processList.contains(expectedString)) {
        throw new GradleException("Expected process output to contain ${expectedString}, but was: ${processList}".toString())
      }
    }
  }
  //create a task for git push - depends on dockerfile
}

task generateManifest() {
  doLast {
    def meta = []
    subprojects.tasks*.getByName('docker').forEach { BuildDockerImageTask dockerTask ->
      meta << [
        file     : "${dockerTask.imageTarFile.name}.gz",
        imageName: dockerTask.dockerImageName,
        tag      : dockerTask.imageTag
      ]
    }

    def jsonFile = project.file("${project.buildDir}/docker/manifest.json")
    project.mkdir(jsonFile.parentFile)
    jsonFile.withWriter("utf-8") { Writer out ->
      out << JsonOutput.toJson(meta)
    }
  }
}

assemble.dependsOn(subprojects.tasks*.getByName('assemble'))
assemble.dependsOn(generateManifest)
