/*
 * Copyright 2020 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import com.thoughtworks.go.build.VerifyJarTask

description = 'GoCD Agent'

sourceSets {
  functionalTest {
    java {
      srcDirs = [file('src/test-functional/java')]
    }
    resources {
      srcDirs = [file('src/test-functional/resources')]
    }

    compileClasspath = sourceSets.main.output + sourceSets.test.output + configurations.functionalTestCompile
    runtimeClasspath = output + compileClasspath + configurations.functionalTestRuntime
  }
}

idea {
  module {
    testSourceDirs += sourceSets.functionalTest.java.srcDirs


    // some hax to fudge the intellij xml
    iml.withXml {
      def node = it.asNode()
      def content = node.component.find { it.'@name' == 'NewModuleRootManager' }.content[0]
      content.sourceFolder.each { sourceFolder ->
        if (sourceFolder.@url?.endsWith('/resources')) {
          sourceFolder.attributes().with {
            boolean isTestSource = (remove('isTestSource') == 'true')
            put('type', isTestSource ? 'java-test-resource' : 'java-resource')
          }
        }
      }
    }
  }
}

configurations {
  functionalTestCompile.extendsFrom testCompile
  functionalTestRuntime.extendsFrom testRuntime
}

dependencies {
  // BC is not really a compile dep, but it's marked as `compileOnly` in other places to avoid leaking it to downstream
  // we mark it as `compile` here so it gets packaged in the resultant jar
  compile group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: project.versions.bouncyCastle
  compile group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: project.versions.bouncyCastle

  compile project(':agent-common')
  compile project(':common')
  compile group: 'org.objenesis', name: 'objenesis', version: project.versions.objenesis
  compile group: 'org.apache.commons', name: 'commons-configuration2', version: project.versions.commonsConfiguration

  compile group: 'org.nanohttpd', name: 'nanohttpd', version: versions.nanohttpd

  testCompile project(path: ':common', configuration: 'testOutput')
  testCompile project(path: ':config:config-api', configuration: 'testOutput')
  testCompile project(':test:test-utils')

  testCompileOnly group: 'junit', name: 'junit', version: project.versions.junit
  testRuntimeOnly group: 'org.junit.vintage', name: 'junit-vintage-engine', version: project.versions.junit5
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: project.versions.junit5
  testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: project.versions.junit5
  testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-migrationsupport', version: project.versions.junit5

  extractedAtTopLevel project(':agent-process-launcher')
  extractedAtTopLevel project(':jar-class-loader')
}

jar {
  archiveClassifier = 'classes'
}

clean {
  delete 'config'
  delete 'felix-cache'
  delete 'main'
  delete 'pipelines'
  delete 'plugins'
  delete 'plugins_work'
  delete 'agent-plugins.zip'
  delete 'tfs-impl.jar'
}

task fatJar(type: Jar) {
  finalizedBy 'verifyJar'
  dependsOn jar, project.configurations.extractedAtTopLevel

  manifest {
    attributes 'Main-Class': 'com.thoughtworks.gocd.Boot'
    attributes 'GoCD-Main-Class': 'com.thoughtworks.go.agent.AgentMain'
    attributes 'Go-Agent-Bootstrap-Class': 'com.thoughtworks.go.agent.AgentProcessParentImpl'
  }

  from(configurations.compile + jar.archivePath) {
    into "lib/"
    rename "go-plugin-activator-${project.version}.jar", "go-plugin-activator.jar"
  }

  from(project.configurations.extractedAtTopLevel.collect { it.isDirectory() ? it : zipTree(it) }) {
    into("/")
  }
}

task verifyJar(type: VerifyJarTask) {
  jarTask = fatJar
  expectedJars = [
    "lib": [
      "agent-${project.version}-classes.jar",
      "agent-common-${project.version}.jar",
      "ant-${project.versions.apacheAnt}.jar",
      "asm-3.3.1.jar",
      "base-${project.version}.jar",
      "bcpkix-jdk15on-${project.versions.bouncyCastle}.jar",
      "bcprov-jdk15on-${project.versions.bouncyCastle}.jar",
      "cglib-2.2.2.jar",
      "cloning-${project.versions.cloning}.jar",
      "commandline-${project.version}.jar",
      "common-${project.version}.jar",
      "commons-beanutils-${project.versions.commonsBeanutils}.jar",
      "commons-codec-${project.versions.commonsCodec}.jar",
      "commons-collections-3.2.2.jar",
      "commons-collections4-${project.versions.commonsCollections4}.jar",
      "commons-configuration2-${project.versions.commonsConfiguration}.jar",
      "commons-digester3-${project.versions.commonsDigester3}.jar",
      "commons-io-${project.versions.commonsIO}.jar",
      "commons-lang3-${project.versions.commonsLang3}.jar",
      "commons-text-1.8.jar",
      "config-api-${project.version}.jar",
      "db-${project.version}.jar",
      "dom4j-1.6.1.jar",
      "domain-${project.version}.jar",
      "guava-${project.versions.guava}.jar",
      "checker-qual-2.11.1.jar",
      "error_prone_annotations-2.3.4.jar",
      "failureaccess-1.0.1.jar",
      "j2objc-annotations-1.3.jar",
      "jsr305-3.0.2.jar",
      "listenablefuture-9999.0-empty-to-avoid-conflict-with-guava.jar",
      "go-plugin-access-${project.version}.jar",
      "go-plugin-activator.jar",
      "go-plugin-api-${project.version}.jar",
      "go-plugin-api-internal-${project.version}.jar",
      "go-plugin-config-repo-${project.version}.jar",
      "go-plugin-domain-${project.version}.jar",
      "go-plugin-infra-${project.version}.jar",
      "gson-${project.versions.gson}.jar",
      "httpclient-${project.versions.apacheHttpComponents}.jar",
      "httpcore-4.4.10.jar",
      "httpmime-${project.versions.apacheHttpComponents}.jar",
      "jackson-annotations-${project.versions.jackson}.jar",
      "jackson-core-${project.versions.jackson}.jar",
      "jackson-databind-${project.versions.jackson}.jar",
      "javax.annotation-api-${project.versions.javaxAnnotation}.jar",
      "javax.inject-1.jar",
      "javax.servlet-api-3.1.0.jar",
      "jcl-over-slf4j-${project.versions.slf4j}.jar",
      "jcommander-${project.versions.jcommander}.jar",
      "jdom2-${project.versions.jdom}.jar",
      "joda-time-${project.versions.jodaTime}.jar",
      "jolt-core-${project.versions.jolt}.jar",
      "json-utils-${project.versions.jolt}.jar",
      "logback-classic-${project.versions.logback}.jar",
      "logback-core-${project.versions.logback}.jar",
      "nanohttpd-2.3.1.jar",
      "objenesis-${project.versions.objenesis}.jar",
      "org.apache.felix.framework-${project.versions.felix}.jar",
      "plugin-metadata-store-${project.version}.jar",
      "quartz-${project.versions.quartz}.jar",
      "semantic-version-${project.versions.semanticVersion}.jar",
      "slf4j-api-${project.versions.slf4j}.jar",
      "spring-aop-${project.versions.spring}.jar",
      "spring-beans-${project.versions.spring}.jar",
      "spring-context-${project.versions.spring}.jar",
      "spring-core-${project.versions.spring}.jar",
      "spring-expression-${project.versions.spring}.jar",
      "spring-tx-${project.versions.spring}.jar",
      "spring-web-${project.versions.spring}.jar",
      "spring-webmvc-${project.versions.spring}.jar",
      "util-${project.version}.jar"
    ]
  ]
}

task functionalTest(type: Test) {
  description = 'Runs the functional tests.'
  group = 'verification'
  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
  mustRunAfter test

  reports {
    html.enabled = true
    junitXml.enabled = true
  }
}

check.dependsOn(functionalTest)

artifacts {
  fatJarConfig fatJar
}
