/*
 * Copyright 2020 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import com.thoughtworks.go.build.VerifyJarTask

description = 'Agent Launcher Jar'

dependencies {
  // BC is not really a compile dep, but it's marked as `compileOnly` in other places to avoid leaking it to downstream
  // we mark it as `compile` here so it gets packaged in the resultant jar
  compile group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: project.versions.bouncyCastle
  compile group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: project.versions.bouncyCastle

  compile project(':agent-common')
  testCompileOnly group: 'junit', name: 'junit', version: project.versions.junit
  testRuntimeOnly group: 'org.junit.vintage', name: 'junit-vintage-engine', version: project.versions.junit5
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: project.versions.junit5
  testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: project.versions.junit5
  testCompile group: 'org.mockito', name: 'mockito-core', version: project.versions.mockito
  testCompile project(':test:test-utils')
}

jar {
  archiveClassifier = 'classes'
}

task fatJar(type: Jar) {
  finalizedBy 'verifyJar'
  dependsOn jar
  manifest {
    attributes 'Go-Agent-Launcher-Class': 'com.thoughtworks.go.agent.launcher.AgentLauncherImpl'
    attributes 'Go-Agent-Launcher-Lib-Dir': 'libs'
  }

  from(configurations.compile + jar.archivePath) { into "libs/" }
}

task verifyJar(type: VerifyJarTask) {
  jarTask = fatJar
  expectedJars = [
      "libs": [
          "agent-common-${project.version}.jar",
          "agent-launcher-${project.version}-classes.jar",
          "ant-${project.versions.apacheAnt}.jar",
          "bcpkix-jdk15on-${project.versions.bouncyCastle}.jar",
          "bcprov-jdk15on-${project.versions.bouncyCastle}.jar",
          "base-${project.version}.jar",
          "commons-io-${project.versions.commonsIO}.jar",
          "commons-lang3-${project.versions.commonsLang3}.jar",
          "commons-codec-${project.versions.commonsCodec}.jar",
          "httpclient-${project.versions.apacheHttpComponents}.jar",
          "httpcore-4.4.10.jar",
          "jcl-over-slf4j-${project.versions.slf4j}.jar",
          "jcommander-${project.versions.jcommander}.jar",
          "joda-time-${project.versions.jodaTime}.jar",
          "logback-classic-${project.versions.logback}.jar",
          "logback-core-${project.versions.logback}.jar",
          "slf4j-api-${project.versions.slf4j}.jar",
      ]
  ]
}

artifacts {
  fatJarConfig fatJar
}
