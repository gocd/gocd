/*
 * Copyright 2016 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

task agentGenericZip(type: Zip) {
  destinationDir file("${project.convention.plugins.get("base").distsDir}/zip")
  archiveName "go-agent-${rootProject.fullVersion}.zip"
  into("go-agent-${project.goVersion}")
  group project.name
  description 'Build the go-agent zip installer'

  // add atleast one file during the configuration phase, or gradle will crib about the task being up-to-date
  from("${rootProject.projectDir}/LICENSE")

  from("${project.projectDir}/go-agent/release") {
    exclude "*.sh"
  }

  from("${project.projectDir}/go-agent/release") {
    include "*.sh"
    fileMode = 0755
  }

  from(configurations.agentBootstrapperJar) {
    rename "(.*)-${project.version}.(.*)", '$1.$2'
  }

  from("${project(':agent-bootstrapper').projectDir}/resources/config/agent-bootstrapper-log4j.properties") {
    into "config"
  }
  from("${project(':agent-launcher').projectDir}/resources/config/agent-launcher-log4j.properties") { into "config" }
  from("${project(':agent').projectDir}/resources/config/agent-log4j.properties") { into "config" }
}

task serverGenericZip(type: Zip) {
  destinationDir file("${project.convention.plugins.get("base").distsDir}/zip")
  archiveName "go-server-${rootProject.fullVersion}.zip"
  into("go-server-${project.goVersion}")
  group project.name
  description = 'Build the go-server zip installer'

  // add atleast one file during the configuration phase, or gradle will crib about the task being up-to-date
  from("${rootProject.projectDir}/LICENSE")
  from("${project(':server').projectDir}/properties/src/config/log4j.properties") { into "config" }

  from("${project.projectDir}/go-server/release") {
    include "*.sh"
    fileMode = 0755
  }

  from("${project.projectDir}/go-server/release") {
    exclude "cruise-config.xml"
  }

  from(configurations.serverJar) {
    rename "(.*)-${project.version}.(.*)", 'go.jar'
  }
}

assemble.dependsOn(":installers:agentGenericZip", ":installers:serverGenericZip")
