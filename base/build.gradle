/*
 * Copyright 2021 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

description = 'GoCD Base'

dependencies {
  providedAtPackageTime project.deps.bouncyCastle
  providedAtPackageTime project.deps.bouncyCastlePkix

  api(project.deps.apacheAnt)  {
    transitive = false
  }
  api project.deps.commonsIO
  api project.deps.commonsLang3
  api project.deps.commonsCodec
  api(project.deps.apacheHttpComponents) {
    exclude(module: 'commons-codec')
    exclude(module: 'commons-logging')
  }
  api project.deps.jodaTime
  api project.deps.slf4jJcl
  api project.deps.logback
  testCompileOnly project.deps.junit4
  testRuntimeOnly project.deps.junit5Vintage
  testImplementation project.deps.junit5
  testRuntimeOnly project.deps.junit5Engine
  testImplementation project.deps.junitExt
  testImplementation project.deps.hamcrest
  testImplementation project.deps.hamcrestLibrary
  testImplementation project.deps.mockito
  testImplementation project.deps.junit5Migration
  testImplementation project.deps.assertJ
}

def generatedResourcesOutput = file("resources-generated")
def generatedResourcesInput = file('resource-templates')

sourceSets {
  main {
    resources {
      output.dir generatedResourcesOutput, builtBy: 'generateResources'
      srcDirs += generatedResourcesOutput
    }
  }
}

clean {
  delete(generatedResourcesOutput)
}

task generateResources {
  inputs.dir(generatedResourcesInput)
  outputs.dir(generatedResourcesOutput)

  def props = [
    'goVersion': rootProject.goVersion,
    'distVersion': rootProject.distVersion,
    'fullVersion': rootProject.fullVersion,
    'gitRevision': rootProject.gitRevision,
    'copyrightYear': rootProject.copyrightYear,
  ]

  inputs.properties(props)

  doFirst {
    project.delete(generatedResourcesOutput)
    generatedResourcesOutput.mkdirs()

    project.copy {
      into generatedResourcesOutput
      from(generatedResourcesInput) {
        expand(props)
      }
    }
  }
}

task prepare {
  dependsOn generateResources
}
