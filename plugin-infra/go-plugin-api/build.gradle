/*
 * Copyright 2020 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'maven-publish'

description = 'Interfaces necessary for GoCD to access plugins'

dependencies {
  compile project(':plugin-infra:go-plugin-api-internal')
  testCompileOnly group: 'junit', name: 'junit', version: project.versions.junit
  testRuntimeOnly group: 'org.junit.vintage', name: 'junit-vintage-engine', version: project.versions.junit5
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: project.versions.junit5
  testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: project.versions.junit5
  testCompile group: 'org.hamcrest', name: 'hamcrest-core', version: project.versions.hamcrest
  testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: project.versions.hamcrest
  testCompile group: 'org.mockito', name: 'mockito-core', version: project.versions.mockito
  testCompile project(':test:test-utils')
}

task sourcesJar(type: Jar, dependsOn: classes) {
  archiveClassifier = 'sources'
  from sourceSets.main.allSource
}

javadoc {
  title = 'GoCD Plugin API'

  options {
    windowTitle = 'GoCD Plugin API'
    docTitle = 'GoCD Plugin API'
    header = 'GoCD Plugin API'
    bottom = "Copyright &copy; ${Calendar.getInstance().get(Calendar.YEAR).toString()} ThoughtWorks, Inc. GoCD Version: ${project.version}"
  }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  archiveClassifier = 'javadoc'
  from javadoc.destinationDir
}

publishing {
  publications {
    maven(MavenPublication) {
      from components.java
    }
  }
}
