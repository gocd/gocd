/*
 * Copyright 2021 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import com.thoughtworks.go.build.VerifyJarTask

description = 'GoCD Agent'

repositories {
  maven { url 'https://jitpack.io' }
}

sourceSets {
  functionalTest {
    java {
      srcDirs = [file('src/test-functional/java')]
    }
    resources {
      srcDirs = [file('src/test-functional/resources')]
    }

    compileClasspath = sourceSets.main.output + sourceSets.test.output + configurations.functionalTestCompileClasspath
    runtimeClasspath = output + compileClasspath + configurations.functionalTestRuntimeClasspath
  }
}

idea {
  module {
    testSourceDirs += sourceSets.functionalTest.java.srcDirs


    // some hax to fudge the intellij xml
    iml.withXml {
      def node = it.asNode()
      def content = node.component.find { it.'@name' == 'NewModuleRootManager' }.content[0]
      content.sourceFolder.each { sourceFolder ->
        if (sourceFolder.@url?.endsWith('/resources')) {
          sourceFolder.attributes().with {
            boolean isTestSource = (remove('isTestSource') == 'true')
            put('type', isTestSource ? 'java-test-resource' : 'java-resource')
          }
        }
      }
    }
  }
}

configurations {
  functionalTestCompileClasspath.extendsFrom testCompileClasspath
  functionalTestRuntimeClasspath.extendsFrom testRuntimeClasspath
  runtimeElements.extendsFrom packagingOnly
}

dependencies {
  packagingOnly project.deps.bouncyCastle
  packagingOnly project.deps.bouncyCastlePkix

  implementation project(':agent-common')
  implementation project(':common')
  implementation project.deps.javaxAnnotation
  implementation project.deps.objenesis
  implementation project.deps.commonsConfiguration
  implementation project.deps.nanohttpd

  testImplementation project(path: ':common', configuration: 'testOutput')
  testImplementation project(path: ':config:config-api', configuration: 'testOutput')
  testImplementation project(':test:test-utils')

  testImplementation project.deps.junit5
  testRuntimeOnly project.deps.junit5Engine
  testImplementation project.deps.springTestJunit5

  extractedAtTopLevel project(':agent-process-launcher')
  extractedAtTopLevel project(':jar-class-loader')
}

jar {
  archiveClassifier = 'classes'
}

clean {
  delete 'config'
  delete 'felix-cache'
  delete 'main'
  delete 'pipelines'
  delete 'plugins'
  delete 'plugins_work'
  delete 'agent-plugins.zip'
  delete 'tfs-impl.jar'
}

task fatJar(type: Jar) {
  finalizedBy 'verifyJar'
  dependsOn jar, project.configurations.extractedAtTopLevel, project.configurations.packagingOnly

  manifest {
    attributes 'Main-Class': 'com.thoughtworks.gocd.Boot'
    attributes 'GoCD-Main-Class': 'com.thoughtworks.go.agent.AgentMain'
    attributes 'Go-Agent-Bootstrap-Class': 'com.thoughtworks.go.agent.AgentProcessParentImpl'
  }

  from(configurations.compileClasspath + project.configurations.packagingOnly + jar.archiveFile) {
    into "lib/"
    rename "go-plugin-activator-${project.version}.jar", "go-plugin-activator.jar"
  }

  from(project.configurations.extractedAtTopLevel.collect { it.isDirectory() ? it : zipTree(it) }) {
    into("/")
  }
}

task verifyJar(type: VerifyJarTask) {
  jarTask = fatJar
  expectedJars = [
    "lib": [
      "FastInfoset-1.2.15.jar",
      "agent-${project.version}-classes.jar",
      "agent-common-${project.version}.jar",
      "animal-sniffer-annotations-1.9.jar",
      "ant-${project.versions.apacheAnt}.jar",
      "base-${project.version}.jar",
      "bcpkix-jdk15on-${project.versions.bouncyCastle}.jar",
      "bcprov-jdk15on-${project.versions.bouncyCastle}.jar",
      "checker-qual-3.12.0.jar",
      "cloning-${project.versions.cloning}.jar",
      "commandline-${project.version}.jar",
      "common-${project.version}.jar",
      "commons-codec-${project.versions.commonsCodec}.jar",
      "commons-collections4-${project.versions.commonsCollections4}.jar",
      "commons-configuration2-${project.versions.commonsConfiguration}.jar",
      "commons-io-${project.versions.commonsIO}.jar",
      "commons-lang3-${project.versions.commonsLang3}.jar",
      "commons-text-${project.versions.commonsText}.jar",
      "config-api-${project.version}.jar",
      "db-${project.version}.jar",
      "dom4j-${project.versions.dom4j}.jar",
      "domain-${project.version}.jar",
      "error_prone_annotations-2.7.1.jar",
      "failureaccess-1.0.1.jar",
      "go-plugin-access-${project.version}.jar",
      "go-plugin-activator.jar",
      "go-plugin-api-${project.version}.jar",
      "go-plugin-api-internal-${project.version}.jar",
      "go-plugin-config-repo-${project.version}.jar",
      "go-plugin-domain-${project.version}.jar",
      "go-plugin-infra-${project.version}.jar",
      "gson-${project.versions.gson}.jar",
      "guava-${project.versions.guava}.jar",
      "httpclient-${project.versions.apacheHttpComponents}.jar",
      "httpcore-4.4.13.jar",
      "httpmime-${project.versions.apacheHttpComponents}.jar",
      "istack-commons-runtime-3.0.7.jar",
      "j2objc-annotations-1.3.jar",
      "jackson-annotations-${project.versions.jackson}.jar",
      "jackson-core-${project.versions.jackson}.jar",
      "jackson-databind-${project.versions.jackson}.jar",
      "javax.activation-api-1.2.0.jar",
      "javax.annotation-api-${project.versions.javaxAnnotation}.jar",
      "javax.inject-1.jar",
      "javax.servlet-api-${project.versions.servletApi}.jar",
      "jaxb-api-${project.versions.jaxb}.jar",
      "jaxb-runtime-${project.versions.jaxb}.jar",
      "jcl-over-slf4j-${project.versions.slf4j}.jar",
      "jcommander-${project.versions.jcommander}.jar",
      "jdom2-${project.versions.jdom}.jar",
      "joda-time-${project.versions.jodaTime}.jar",
      "jolt-core-${project.versions.jolt}.jar",
      "json-utils-${project.versions.jolt}.jar",
      "jsr305-3.0.2.jar",
      "listenablefuture-9999.0-empty-to-avoid-conflict-with-guava.jar",
      "logback-classic-${project.versions.logback}.jar",
      "logback-core-${project.versions.logback}.jar",
      "nanohttpd-${project.versions.nanohttpd}.jar",
      "objenesis-${project.versions.objenesis}.jar",
      "org.apache.felix.framework-${project.versions.felix}.jar",
      "plugin-metadata-store-${project.version}.jar",
      "quartz-${project.versions.quartz}.jar",
      "semantic-version-${project.versions.semanticVersion}.jar",
      "slf4j-api-${project.versions.slf4j}.jar",
      "spring-aop-${project.versions.spring}.jar",
      "spring-beans-${project.versions.spring}.jar",
      "spring-context-${project.versions.spring}.jar",
      "spring-core-${project.versions.spring}.jar",
      "spring-expression-${project.versions.spring}.jar",
      "spring-tx-${project.versions.spring}.jar",
      "spring-web-${project.versions.spring}.jar",
      "spring-webmvc-${project.versions.spring}.jar",
      "stax-ex-1.8.jar",
      "txw2-${project.versions.jaxb}.jar",
      "util-${project.version}.jar"
    ]
  ]
}

task functionalTest(type: Test) {
  description = 'Runs the functional tests.'
  group = 'verification'
  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
  mustRunAfter test

  reports {
    html.required = true
    junitXml.required = true
  }
}

check.dependsOn(functionalTest)

artifacts {
  fatJarConfig fatJar
}
