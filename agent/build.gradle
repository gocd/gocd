import com.thoughtworks.go.build.VerifyJarTask

/*
 * Copyright 2016 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

description = 'GoCD Agent'

sourceSets {
  test {
    java {
      srcDirs = ['test/functional', 'test/unit']
    }
  }
}

dependencies {
  compile project(':agent-common')
  compile project(':common')
  compile group: 'org.objenesis', name: 'objenesis', version: '1.2'
  compile group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.47'
  compile group: 'org.eclipse.jetty.websocket', name: 'websocket-client', version: versions.jetty
  compile group: 'commons-configuration', name: 'commons-configuration', version: '1.10'
  testCompile project(path: ':common', configuration: 'testOutput')
  testCompile project(path: ':config-api', configuration: 'testOutput')
  testCompile project(':test-utils')
  packagingOnly project(path: ':tfs-impl', configuration: 'fatJarConfig')
  extractedAtTopLevel project(':agent-process-launcher')
  extractedAtTopLevel group: 'one-jar', name: 'boot', version: '0.96'
}

jar {
  classifier 'classes'
}

clean.doFirst {
  delete 'config'
  delete 'felix-cache'
  delete 'main'
  delete 'pipelines'
  delete 'plugins'
  delete 'plugins_work'
}

task fatJar(type: Jar, dependsOn: [':agent:jar', ':agent-process-launcher:jar', ':tfs-impl:fatJar']) {
  manifest {
    attributes 'Main-Class': 'com.simontuffs.onejar.Boot'
    attributes 'One-Jar-Main-Class': 'com.thoughtworks.go.agent.AgentMain'
    attributes 'Go-Agent-Bootstrap-Class': 'com.thoughtworks.go.agent.AgentProcessParentImpl'
  }

  doFirst {
    from(configurations.packagingOnly + configurations.compile) {
      into "lib/"
      rename "tfs-impl-${project.version}.jar", 'tfs-impl.jar'
      rename "go-plugin-activator-${project.version}.jar", "go-plugin-activator.jar"
    }

    from(jar.archivePath) {
      into('main/')
    }

    with(copySpec {
      from { configurations.extractedAtTopLevel.collect { it.isDirectory() ? it : zipTree(it) } }
      include '**/*.class'
      exclude 'OneJar.class'
      into("/")
    })
  }

  doLast {
    verifyJar.execute()
  }
}

task verifyJar(type: VerifyJarTask) {
  jarTask = fatJar
  expectedJars = [
      "main": [
          "agent-${project.version}-classes.jar",
      ],
      "lib" : [
          "agent-common-${project.version}.jar",
          "aopalliance-1.0.jar",
          "base-${project.version}.jar",
          "bcprov-jdk15on-1.47.jar",
          "cglib-nodep-2.2.jar",
          "cloning-1.7.3.jar",
          "common-${project.version}.jar",
          "commons-beanutils-1.7.0.jar",
          "commons-codec-1.9.jar",
          "commons-collections-3.2.2.jar",
          "commons-configuration-1.10.jar",
          "commons-digester-1.8.jar",
          "commons-io-2.5.jar",
          "commons-lang-2.6.jar",
          "commons-logging-1.2.jar",
          "config-api-${project.version}.jar",
          "db-${project.version}.jar",
          "dom4j-1.6.1.jar",
          "fluent-hc-4.5.2.jar",
          "freemarker-2.3.18.jar",
          "go-plugin-access-${project.version}.jar",
          "go-plugin-activator.jar",
          "go-plugin-api-${project.version}.jar",
          "go-plugin-api-internal-${project.version}.jar",
          "go-plugin-infra-${project.version}.jar",
          "gson-2.3.1.jar",
          "httpclient-4.5.2.jar",
          "httpcore-4.4.4.jar",
          "httpmime-4.5.2.jar",
          "jackson-annotations-2.5.0.jar",
          "jackson-core-2.5.0.jar",
          "jackson-databind-2.5.0.jar",
          "javasysmon-0.3.6.jar",
          "javax.inject-1.jar",
          "javax.servlet-api-3.1.0.jar",
          "jcommander-1.48.jar",
          "jdom-1.0.jar",
          "jettison-1.2.jar",
          "jetty-io-${versions.jetty}.jar",
          "jetty-util-${versions.jetty}.jar",
          "joda-time-2.3.jar",
          "jolt-core-0.0.20.jar",
          "json-utils-0.0.20.jar",
          "kxml2-2.3.0.jar",
          "kxml2-min-2.3.0.jar",
          "log4j-1.2.17.jar",
          "objenesis-1.2.jar",
          "org.apache.felix.framework-4.2.1.jar",
          "quartz-1.6.5.jar",
          "slf4j-api-1.7.21.jar",
          "slf4j-log4j12-1.7.21.jar",
          "spring-aop-3.1.3.RELEASE.jar",
          "spring-asm-3.1.3.RELEASE.jar",
          "spring-beans-3.1.3.RELEASE.jar",
          "spring-context-3.1.3.RELEASE.jar",
          "spring-context-support-3.1.3.RELEASE.jar",
          "spring-core-3.1.3.RELEASE.jar",
          "spring-expression-3.1.3.RELEASE.jar",
          "spring-jdbc-3.1.3.RELEASE.jar",
          "spring-orm-3.1.3.RELEASE.jar",
          "spring-tx-3.1.3.RELEASE.jar",
          "spring-web-3.1.3.RELEASE.jar",
          "spring-webmvc-3.1.3.RELEASE.jar",
          "stax-api-1.0.1.jar",
          "tfs-impl.jar",
          "util-${project.version}.jar",
          "websocket-api-${versions.jetty}.jar",
          "websocket-client-${versions.jetty}.jar",
          "websocket-common-${versions.jetty}.jar",
          "xml-apis-1.0.b2.jar",
      ]
  ]
}

artifacts {
  fatJarConfig fatJar
}
