/*
 * Copyright 2017 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import com.thoughtworks.go.build.VerifyJarTask

description = 'GoCD Agent'

apply plugin: 'com.thoughtworks.go.test-sets'

testSets {
  functionalTest {
    dirName = 'test-functional'
  }
}

dependencies {
  compile project(':agent-common')
  compile project(':common')
  compile group: 'org.objenesis', name: 'objenesis', version: project.versions.objenesis
  compile group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.47'
  compile group: 'commons-configuration', name: 'commons-configuration', version: '1.10'
  compile(group: 'org.eclipse.jetty.websocket', name: 'websocket-client', version: versions.jetty){
    exclude module: 'jetty-xml'
  }

  compile group: 'org.nanohttpd', name: 'nanohttpd', version: versions.nanohttpd

  testCompile project(path: ':common', configuration: 'testOutput')
  testCompile project(path: ':config:config-api', configuration: 'testOutput')
  testCompile project(':test:test-utils')

  testCompileOnly group: 'junit', name: 'junit', version: project.versions.junit
  testRuntimeOnly group: 'org.junit.vintage', name: 'junit-vintage-engine', version: project.versions.junit5
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: project.versions.junit5

  extractedAtTopLevel project(':agent-process-launcher')
  extractedAtTopLevel project(':jar-class-loader')

  functionalTestCompile sourceSets.test.output
}

jar {
  classifier 'classes'
}

clean {
  delete 'config'
  delete 'felix-cache'
  delete 'main'
  delete 'pipelines'
  delete 'plugins'
  delete 'plugins_work'
  delete 'agent-plugins.zip'
  delete 'tfs-impl.jar'
}

task fatJar(type: Jar) {
  finalizedBy 'verifyJar'
  dependsOn jar, project.configurations.extractedAtTopLevel

  manifest {
    attributes 'Main-Class': 'com.thoughtworks.gocd.Boot'
    attributes 'GoCD-Main-Class': 'com.thoughtworks.go.agent.AgentMain'
    attributes 'Go-Agent-Bootstrap-Class': 'com.thoughtworks.go.agent.AgentProcessParentImpl'
  }

  from(configurations.compile + jar.archivePath) {
    into "lib/"
    rename "go-plugin-activator-${project.version}.jar", "go-plugin-activator.jar"
  }

  from(project.configurations.extractedAtTopLevel.collect { it.isDirectory() ? it : zipTree(it) }) {
    into("/")
  }
}

task verifyJar(type: VerifyJarTask) {
  jarTask = fatJar
  expectedJars = [
    "lib": [
      "agent-${project.version}-classes.jar",
      "agent-common-${project.version}.jar",
      "base-${project.version}.jar",
      "bcprov-jdk15on-1.47.jar",
      "cloning-1.7.3.jar",
      "commandline-${project.version}.jar",
      "common-${project.version}.jar",
      "commons-beanutils-1.7.0.jar",
      "commons-codec-${project.versions.commonsCodec}.jar",
      "commons-collections-3.2.2.jar",
      "commons-configuration-1.10.jar",
      "commons-digester-1.8.jar",
      "commons-io-${project.versions.commonsIO}.jar",
      "commons-lang-${project.versions.commonsLang}.jar",
      "config-api-${project.version}.jar",
      "db-${project.version}.jar",
      "dom4j-1.6.1.jar",
      "domain-${project.version}.jar",
      "go-plugin-access-${project.version}.jar",
      "go-plugin-activator.jar",
      "go-plugin-api-${project.version}.jar",
      "go-plugin-api-internal-${project.version}.jar",
      "go-plugin-domain-${project.version}.jar",
      "go-plugin-infra-${project.version}.jar",
      "gson-${project.versions.gson}.jar",
      "httpclient-${project.versions.apacheHttpComponents}.jar",
      "httpcore-4.4.7.jar",
      "httpmime-${project.versions.apacheHttpComponents}.jar",
      "jackson-annotations-2.5.0.jar",
      "jackson-core-2.5.0.jar",
      "jackson-databind-2.5.0.jar",
      "javasysmon-0.3.6.jar",
      "javax.inject-1.jar",
      "javax.servlet-api-3.1.0.jar",
      "jcl-over-slf4j-${project.versions.slf4j}.jar",
      "jcommander-${project.versions.jcommander}.jar",
      "jdom-2.0.2.jar",
      "jetty-io-${versions.jetty}.jar",
      "jetty-util-${versions.jetty}.jar",
      "jetty-client-${versions.jetty}.jar",
      "jetty-http-${versions.jetty}.jar",
      "joda-time-2.3.jar",
      "jolt-core-0.1.0.jar",
      "json-utils-0.1.0.jar",
      "logback-classic-${project.versions.logback}.jar",
      "logback-core-${project.versions.logback}.jar",
      "nanohttpd-2.3.1.jar",
      "objenesis-${project.versions.objenesis}.jar",
      "org.apache.felix.framework-${project.versions.felix}.jar",
      "plugin-metadata-store-${project.version}.jar",
      "quartz-1.6.5.jar",
      "slf4j-api-${project.versions.slf4j}.jar",
      "spring-aop-${project.versions.spring}.jar",
      "spring-beans-${project.versions.spring}.jar",
      "spring-context-${project.versions.spring}.jar",
      "spring-core-${project.versions.spring}.jar",
      "spring-expression-${project.versions.spring}.jar",
      "spring-tx-${project.versions.spring}.jar",
      "spring-web-${project.versions.spring}.jar",
      "spring-webmvc-${project.versions.spring}.jar",
      "util-${project.version}.jar",
      "websocket-api-${versions.jetty}.jar",
      "websocket-client-${versions.jetty}.jar",
      "websocket-common-${versions.jetty}.jar",
      "xml-apis-1.0.b2.jar",
    ]
  ]
}

check.dependsOn(functionalTest)

artifacts {
  fatJarConfig fatJar
}
