/*
 * Copyright 2021 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

description = 'GoCD Server used for development'

configurations {
  copyOnly {
    transitive = false
  }
}

dependencies {
  implementation project(':server')
  implementation project(':api').subprojects
  implementation project(':spark').subprojects
  implementation project.deps.jetty
  implementation project.deps.jettyPlus
  implementation project.deps.jettyJmx
  implementation project.deps.jettyServlets
  implementation project.deps.jettyUtil
  implementation project.deps.jettyDeploy
  implementation project.deps.jettyWebsocket

  implementation(project.deps.jgitServer) {
    exclude(module: 'jsch')
    exclude(module: 'jzlib')
  }
  implementation project(':jetty9')
  implementation project.deps.servletApi
  implementation project.deps.jruby
  implementation project.deps.rack
  implementation project.deps.bouncyCastle
  implementation project.deps.bouncyCastlePkix
  implementation project.deps.slf4j

  copyOnly project(path: ':tw-go-plugins', configuration: 'pluginsZipConfig')
  copyOnly project(':plugin-infra:go-plugin-activator')
}

def generatedResourcesOutput = file("resources-generated")

sourceSets {
  main {
    resources {
      output.dir generatedResourcesOutput, builtBy: 'generateResources'
      srcDirs += generatedResourcesOutput
    }
  }
}

clean {
  delete(generatedResourcesOutput)
}

task generateResources(type: Copy) {
  outputs.dir(generatedResourcesOutput)

  into generatedResourcesOutput
  from(project.configurations.copyOnly) {
    rename "(.*)-${project.version}.(jar|zip)", '$1.$2'
  }
}

processResources {
  dependsOn generateResources
}

task prepare {
  dependsOn generateResources
}
