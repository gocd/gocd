/*
 * Copyright 2021 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import com.thoughtworks.go.build.VerifyJarTask

description = 'GoCD TFS SDK wrapper v14'

dependencies {
  implementation project(':common')
  implementation files('tfssdk/lib/com.microsoft.tfs.sdk-14.135.0.jar')
  // not an implementation dependency, but some classloader hell
  implementation project.deps.xalan
  testCompileOnly project.deps.junit4
  testRuntimeOnly project.deps.junit5Vintage
  testImplementation project.deps.junit5
  testRuntimeOnly project.deps.junit5Engine
  testImplementation project.deps.hamcrest
  testImplementation project.deps.mockito
  testImplementation project.deps.assertJ
}

jar {
  archiveClassifier = 'classes'
}

task fatJar(type: Jar) {
  finalizedBy 'verifyJar'
  dependsOn jar
  from(jar.archiveFile) { into('lib/') }

  from(project.configurations.compileClasspath.findAll {
    it.absolutePath.contains('tfssdk') || it.absolutePath.contains('xalan') || it.absolutePath.contains('serializer')
  }) {
    into 'lib/'
  }

  from("${project.projectDir}/tfssdk/native") {
    include "**/*.*"
    into "tfssdk/native"
  }
}

task verifyJar(type: VerifyJarTask) {
  jarTask = fatJar
  expectedJars = [
      "lib": [
          "tfs-impl-14-${project.version}-classes.jar",
          "com.microsoft.tfs.sdk-14.135.0.jar",
      ]
  ]
}

artifacts {
  fatJarConfig fatJar
}
