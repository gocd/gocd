/*
 * Copyright 2017 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import com.thoughtworks.go.build.VerifyJarTask

description = 'GoCD Agent Bootstrapper'

dependencies {
  compile project(':agent-common')
  compile group: 'com.apple', name: 'AppleJavaExtensions', version: '1.4'
  compile group: 'commons-codec', name: 'commons-codec', version: '1.10'
  testCompile project(':test-utils')
  testCompile group: 'org.mockito', name: 'mockito-core', version: project.versions.mockito
  testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: project.versions.hamcrest
  packagingOnly project(path: ':agent-launcher', configuration: 'fatJarConfig')
  extractedAtTopLevel project(':jar-class-loader')
}

jar {
  classifier 'classes'
}

task fatJar(type: Jar) {
  dependsOn jar, configurations.packagingOnly, configurations.extractedAtTopLevel

  manifest {
    attributes 'Main-Class': 'com.thoughtworks.gocd.Boot'
    attributes 'GoCD-Main-Class': 'com.thoughtworks.go.agent.bootstrapper.AgentBootstrapper'
  }

  from(configurations.compile + jar.archivePath) { into "lib/" }

  from(configurations.packagingOnly) {
    into "defaultFiles/"
    rename "(.*)-${project.version}.jar", '$1.jar'
  }

  from(configurations.extractedAtTopLevel.collect { it.isDirectory() ? it : zipTree(it) }) {
    into("/")
  }
}

task verifyJar(type: VerifyJarTask) {
  jarTask = fatJar
  expectedJars = [
    'defaultFiles': [
      "agent-launcher.jar",
    ],
    'lib'         : [
      "agent-bootstrapper-${project.version}-classes.jar",
      "agent-common-${project.version}.jar",
      "AppleJavaExtensions-1.4.jar",
      "base-${project.version}.jar",
      "commons-codec-1.10.jar",
      "commons-io-2.5.jar",
      "commons-lang-2.6.jar",
      "httpclient-4.5.2.jar",
      "httpcore-4.4.4.jar",
      "jcl-over-slf4j-${project.versions.slf4j}.jar",
      "jcommander-1.60.jar",
      "joda-time-2.3.jar",
      "logback-classic-${project.versions.logback}.jar",
      "logback-core-${project.versions.logback}.jar",
      "slf4j-api-${project.versions.slf4j}.jar",
    ]
  ]
}

artifacts {
  fatJarConfig fatJar
}
