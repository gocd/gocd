/*
 * Copyright 2018 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


import com.github.jk1.license.filter.LicenseBundleNormalizer
import com.github.jk1.license.render.JsonReportRenderer
import com.thoughtworks.go.build.VerifyJarTask

description = 'GoCD Server Launcher'
apply plugin: 'base'
apply plugin: 'com.github.jk1.dependency-license-report'

licenseReport {
  renderers = [new JsonReportRenderer("index.json", false)]
  excludeGroups = ["com.thoughtworks.go"]
  filters = [new LicenseBundleNormalizer()]
  outputDir = file("${project.buildDir}/reports/dependency-license")
  configurations = ['compile', 'compileOnly', 'packagingInLibDir']
}

configurations {
  extractedAtTopLevel { transitive = false }
  fatJarConfig
  packagingInLibDir.extendsFrom packagingOnly
  packagingInDefaultFilesDir.extendsFrom packagingOnly

  packagingInMainJarTopLevel {
    transitive = false
  }

  packagingInMainJarCommonJar {
    transitive = false
  }

  packagingInMainJarBaseJar {
    transitive = false
  }

  packagingInMainJarServerJar {
    transitive = false
  }

  pluginsZip.extendsFrom packagingOnly
}

dependencies {

  packagingInDefaultFilesDir project(path: ':agent-launcher', configuration: 'fatJarConfig')
  packagingInDefaultFilesDir project(path: ':agent', configuration: 'fatJarConfig')
  packagingInDefaultFilesDir project(path: ':server', configuration: 'warConfig')
  packagingInDefaultFilesDir project(path: ':tfs-impl:tfs-impl-14', configuration: 'fatJarConfig')

  packagingInLibDir project(path: ':tfs-impl:tfs-impl-14', configuration: 'fatJarConfig')

  packagingInLibDir(project(path: ':plugin-infra:go-plugin-activator')) {
    transitive = false
  }
  packagingInLibDir group: 'javax.servlet', name: 'javax.servlet-api', version: project.versions.servletApi
  packagingInLibDir group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: project.versions.bouncyCastle
  packagingInLibDir group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: project.versions.bouncyCastle
  packagingInLibDir group: 'commons-io', name: 'commons-io', version: project.versions.commonsIO
  packagingInLibDir group: 'org.apache.commons', name: 'commons-lang3', version: project.versions.commonsLang3
  packagingInLibDir group: 'joda-time', name: 'joda-time', version: project.versions.jodaTime

  // not really compile dependencies, but we want to centralize all logging using logback
  packagingInLibDir(group: 'org.slf4j', name: 'slf4j-api', version: project.versions.slf4j)
  packagingInLibDir(group: 'org.slf4j', name: 'log4j-over-slf4j', version: project.versions.slf4j)
  packagingInLibDir(group: 'org.slf4j', name: 'jcl-over-slf4j', version: project.versions.slf4j)
  packagingInLibDir(group: 'org.slf4j', name: 'jul-to-slf4j', version: project.versions.slf4j)
  packagingInLibDir(group: 'ch.qos.logback', name: 'logback-classic', version: project.versions.logback)

  pluginsZip project(path: ':tw-go-plugins', configuration: 'pluginsZipConfig')

  packagingInMainJarTopLevel project(':jetty9')
  packagingInMainJarTopLevel project(':app-server')
  packagingInMainJarCommonJar project(':common')
  packagingInMainJarBaseJar project(':base')
  packagingInMainJarServerJar project(':server')

  extractedAtTopLevel project(':jar-class-loader')
}

task mainJar(type: Jar) {
  dependsOn project.configurations.packagingInMainJarBaseJar, project.configurations.packagingInMainJarCommonJar, project.configurations.packagingInMainJarTopLevel, project.configurations.packagingInMainJarServerJar
  archiveClassifier = 'main'

  from(project.configurations.packagingInMainJarTopLevel.collect { it.isDirectory() ? it : zipTree(it) }) {
    into('/')
  }

  from(project.configurations.packagingInMainJarCommonJar.collect { it.isDirectory() ? it : zipTree(it) }) {
    include "**/SubprocessLogger*.class"
    include "**/CurrentProcess*.class"
    include "**/validators/*.class"
    include "**/Environment*.class"
    include "**/CommandLineException.class"

    exclude "**/domain/*.class"
    exclude "**/validation/*.class"
    exclude "**/remote/*.class"
    exclude "**/remote/work/*.class"
    into('/')
  }

  from(project.configurations.packagingInMainJarBaseJar.collect { it.isDirectory() ? it : zipTree(it) }) {
    include "**/LogHelper.class"
    include "**/AssertJava.class"
    include "**/JavaVersion.class"
    include "**/LogConfigurator*.class"
    include "**/OperatingSystem.class"
    include "**/SystemEnvironment*.class"
    include "**/ConfigDirProvider.class"
    include "**/validators/*.class"
    include "**/SystemUtil.class"
    include "**/Os.class"
    include "**/ExceptionUtils.class"
    include "**/StreamConsumer.class"
    include "**/InMemoryConsumer.class"
    include "**/InMemoryStreamConsumer.class"
    include "**/ConsoleOutputStreamConsumer.class"
    include "**/StringUtil.class"

    into('/')
  }

  from(project.configurations.packagingInMainJarServerJar.collect { it.isDirectory() ? it : zipTree(it) }) {
    include "**/GoServer*.class"
    include "**/BaseUrlProvider*.class"
    include "**/GoLauncher*.class"
    into('/')
  }

  from("${project(':server').projectDir}/src/main/resources/config/logback.xml") { into 'config' }
}

task h2deltasZip(type: Zip) {
  archiveBaseName = 'h2deltas'
  destinationDirectory = file("${project.buildDir}/${project.libsDirName}")
  from("${project(':server').projectDir}/db/migrate/h2deltas") { into '/h2deltas' }
}

task h2dbZip(type: Zip) {
  archiveBaseName = 'h2db'
  destinationDirectory = file("${project.buildDir}/${project.libsDirName}")
  from("${project(':server').projectDir}/db/dbtemplate/h2db") { into '/h2db' }
}

def commandRepoCheckoutDir = file("${project.buildDir}/go-command-repo")

task cloneCommandRepository(type: Exec) {
  onlyIf { !project.hasProperty('fastBuild') }
  outputs.upToDateWhen { false }
  doFirst { execTask ->
    if (commandRepoCheckoutDir.exists()) {
      execTask.workingDir = commandRepoCheckoutDir
      execTask.commandLine "git", "pull"
    } else {
      execTask.commandLine "git", "clone", project.commandRepoUrl, commandRepoCheckoutDir
    }
  }
}

task defaultCommandSnippetsZip(type: Zip, dependsOn: [':server-launcher:cloneCommandRepository']) {
  archiveBaseName = 'defaultCommandSnippets'
  destinationDirectory = file("${project.buildDir}/${project.libsDirName}")
  from("${rootProject.rootDir}/LICENSE") {
    into "/"
  }
  from(commandRepoCheckoutDir) { into '/' }
  // because .git is in global excludes
  from("${commandRepoCheckoutDir}/.git") { into '/.git' }
}

clean {
  delete 'defaultFiles'
}

task fatJar(type: Jar) {
  finalizedBy 'verifyFatJar'
  dependsOn ':server:jar'
  dependsOn ':server:war'
  dependsOn configurations.packagingInDefaultFilesDir, h2dbZip, h2deltasZip, defaultCommandSnippetsZip, configurations.pluginsZip,
    (project(':jetty9').configurations.compileOnly - project(':jetty9').configurations.compile),
    configurations.packagingInLibDir, mainJar, configurations.extractedAtTopLevel

  manifest {
    attributes 'Main-Class': 'com.thoughtworks.gocd.Boot'
    attributes 'GoCD-Main-Class': 'com.thoughtworks.go.server.util.GoLauncher'
  }

  from(configurations.packagingInDefaultFilesDir) {
    into "defaultFiles/"
    rename "(.*)-${project.version}.(.*)", '$1.$2'
  }

  from(project(':server').war.archivePath) {
    into "defaultFiles/"
    rename "(.*)-${project.version}.(.*)", '$1.$2'
  }

  from("${project(':installers').projectDir}/go-server/release/cruise-config.xml") { into 'defaultFiles/config' }
  from("${project(':server').projectDir}/config/jetty.xml") { into 'defaultFiles/config' }
  from("${project(':server').projectDir}/config/go_update_server.pub") { into 'defaultFiles/config' }
  from("${project(':config:config-server').projectDir}/src/main/resources/cruise-config.xsd") { into 'defaultFiles/config' }
  from(h2dbZip) { into 'defaultFiles' rename "(.*)-${project.version}.zip", '$1.zip' }
  from(h2deltasZip) { into 'defaultFiles' rename "(.*)-${project.version}.zip", '$1.zip' }
  from(defaultCommandSnippetsZip) { into 'defaultFiles' rename "(.*)-${project.version}.zip", '$1.zip' }
  from(configurations.pluginsZip) { into 'defaultFiles' rename "(.*)-${project.version}.zip", '$1.zip' }

  from(project(':jetty9').configurations.compileOnly - project(':jetty9').configurations.compile) {
    into 'lib/'
  }

  from(configurations.packagingInLibDir) {
    into 'lib/'
    rename "tfs-impl-(.*)-${project.version}.jar", 'tfs-impl-$1.jar'
    rename "go-plugin-activator-${project.version}.jar", "go-plugin-activator.jar"
  }

  from(mainJar.archivePath) {
    into('lib/')
  }

  from(configurations.extractedAtTopLevel.collect { it.isDirectory() ? it : zipTree(it) }) {
    into("/")
  }

  from("${rootProject.rootDir}/LICENSE") {
    into "/"
  }
}

task verifyFatJar(type: VerifyJarTask) {
  jarTask = fatJar
  expectedJars = [
    "defaultFiles": [
      "agent-launcher.jar",
      "agent.jar",
      "tfs-impl-14.jar",
      "plugins.zip",
      "h2deltas.zip",
      "h2db.zip",
      "defaultCommandSnippets.zip"
    ],
    "lib"         : [
      "bcpkix-jdk15on-${project.versions.bouncyCastle}.jar",
      "bcprov-jdk15on-${project.versions.bouncyCastle}.jar",
      "commons-io-${project.versions.commonsIO}.jar",
      "commons-lang3-${project.versions.commonsLang3}.jar",
      "go-plugin-activator.jar",
      "javax.servlet-api-3.1.0.jar",
      "jcl-over-slf4j-${project.versions.slf4j}.jar",
      "jetty-continuation-${versions.jetty}.jar",
      "jetty-http-${versions.jetty}.jar",
      "jetty-io-${versions.jetty}.jar",
      "jetty-jmx-${versions.jetty}.jar",
      "jetty-jndi-${versions.jetty}.jar",
      "jetty-plus-${versions.jetty}.jar",
      "jetty-security-${versions.jetty}.jar",
      "jetty-server-${versions.jetty}.jar",
      "jetty-deploy-${versions.jetty}.jar",
      "jetty-servlet-${versions.jetty}.jar",
      "jetty-servlets-${versions.jetty}.jar",
      "jetty-util-${versions.jetty}.jar",
      "jetty-webapp-${versions.jetty}.jar",
      "jetty-xml-${versions.jetty}.jar",
      "jetty-client-${versions.jetty}.jar",
      "joda-time-${project.versions.jodaTime}.jar",
      "jul-to-slf4j-${project.versions.slf4j}.jar",
      "log4j-over-slf4j-${project.versions.slf4j}.jar",
      "logback-classic-${project.versions.logback}.jar",
      "logback-core-${project.versions.logback}.jar",
      "server-launcher-${project.version}-main.jar",
      "slf4j-api-${project.versions.slf4j}.jar",
      "tfs-impl-14.jar",
      "websocket-api-${versions.jetty}.jar",
      "websocket-client-${versions.jetty}.jar",
      "websocket-common-${versions.jetty}.jar",
      "websocket-server-${versions.jetty}.jar",
      "websocket-servlet-${versions.jetty}.jar"
    ]
  ]
}

artifacts {
  fatJarConfig fatJar
}
