import org.apache.tools.ant.filters.FixCrLfFilter

/*
 * Copyright 2016 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

@ParallelizableTask
class WindowsPackagingTask extends DefaultTask {
  @Input
  String packageName
  @Input
  String version
  @Input
  String distVersion

  Closure<Task> beforePackage

  @OutputFile
  public File getOutputFile() {
    project.file("${project.convention.plugins.get("base").distsDir}/${packageName}-${version}-${distVersion}.exe")
  }

  WindowsPackagingTask() {
    outputs.upToDateWhen { false }
    prepareFileSystem()
    buildPackage()
  }

  def buildRoot() {
    project.file("${project.buildDir}/${packageName}/win/BUILD_ROOT")
  }

  def versionedDir() {
    project.file("${buildRoot()}/${packageName}-${version}")
  }

  def prepareFileSystem() {
    doLast {
      buildRoot().deleteDir()
      buildRoot().mkdirs()
      project.convention.plugins.get("base").distsDir.mkdirs()

      project.exec {
        commandLine "bash", "-c", "set -o pipefail; curl --silent --fail ${jreLocation()} | tar -zxf - -C ${buildRoot()}"
        workingDir buildRoot()

        if (project.logger.isInfoEnabled()) {
          standardOutput = System.out
          errorOutput = System.err
        } else {
          standardOutput = new ByteArrayOutputStream()
          errorOutput = new ByteArrayOutputStream()
        }
      }
    }

    doLast {
      beforePackage.call()
    }
  }

  String jreLocation() {
    if (!System.getenv('WINDOWS_JRE_URL')) {
      throw new RuntimeException("Please specify environment variable WINDOWS_JRE_URL")
    }

    System.getenv('WINDOWS_JRE_URL')
  }


  def buildPackage() {
    doLast {
      def names = []
      buildRoot().eachDirMatch(~/jre.*/, { names << it })
      def jreDir = names.first()

      def env = [
          'BINARY_SOURCE_DIR': versionedDir(),
          'LIC_FILE'         : 'LICENSE.dos',
          'NAME'             : packageName.replaceAll(/^go-/, '').capitalize(),
          'MODULE'           : packageName.replaceAll(/^go-/, ''),
          'GO_ICON'          : project.file('windows-shared/gocd.ico').absolutePath,
          'VERSION'          : "${version}-${distVersion}",
          'REGVER'           : "${version}${distVersion.padRight(5, '0')}".replaceAll(/\./, ''),
          'JAVA'             : 'jre',
          'JAVASRC'          : jreDir,
          'DISABLE_LOGGING'  : (System.getenv('DISABLE_WIN_INSTALLER_LOGGING') || 'false'),
          'OUTDIR'           : project.convention.plugins.get("base").distsDir
      ]

      project.exec {
        commandLine "makensis", "-V4", "-NOCD", project.file("${packageName}/win/${packageName}.nsi")
        workingDir buildRoot()
        environment env

        if (project.logger.isInfoEnabled()) {
          standardOutput = System.out
          errorOutput = System.err
        } else {
          standardOutput = new ByteArrayOutputStream()
          errorOutput = new ByteArrayOutputStream()
        }
      }
    }
  }
}

task agentWindowsExe(type: WindowsPackagingTask, dependsOn: [':agent-bootstrapper:fatJar']) {
  packageName 'go-agent'
  version rootProject.version
  distVersion rootProject.distVersion
  beforePackage {
    project.copy {
      from project.rootProject.file('LICENSE')
      into(versionedDir())
    }

    project.copy {
      from project.rootProject.file('LICENSE')
      into(versionedDir())
      filter(FixCrLfFilter.class, eol: FixCrLfFilter.CrLf.DOS)
      rename "LICENSE", "LICENSE.DOS"
    }

    project.copy { from('windows-shared/JavaHome.ini') into(buildRoot()) }
    project.copy { from('go-agent/win/ServerIP.ini') into(buildRoot()) }

    project.copy {
      from(configurations.agentBootstrapperJar.singleFile.path) into(versionedDir()) rename ".*", "agent-bootstrapper.jar"
    }
    project.copy { from('agent/release/agent.cmd') into(versionedDir()) }
    project.copy { from('agent/release/start-agent.bat') into(versionedDir()) }
    project.copy { from('agent/release/stop-agent.bat') into(versionedDir()) }
    project.copy { from('agent/release/README.md') into(versionedDir()) }
    project.copy { from('agent/win/cruisewrapper.exe') into(versionedDir()) }

    project.copy {
      from(project(':agent').file('properties/log4j.properties')) into("${versionedDir()}/config/log4j.properties")
    }

    project.copy { from('go-agent/win/config') into("${versionedDir()}/config") }
    project.copy { from('go-agent/win/lib') into("${versionedDir()}/lib") }
  }
}

task serverWindowsExe(type: WindowsPackagingTask, dependsOn: [':server-launcher:fatJar']) {
  packageName 'go-server'
  version rootProject.version
  distVersion rootProject.distVersion

  beforePackage {
    project.copy {
      from project.rootProject.file('LICENSE')
      into(versionedDir())
    }

    project.copy {
      from project.rootProject.file('LICENSE')
      into(versionedDir())
      filter(FixCrLfFilter.class, eol: FixCrLfFilter.CrLf.DOS)
      rename "LICENSE", "LICENSE.DOS"
    }

    project.copy { from('windows-shared/JavaHome.ini') into(buildRoot()) }

    project.copy { from(configurations.serverJar.singleFile.path) into(versionedDir()) rename ".*", "go.jar" }

    project.copy { from('go-server/release/server.cmd') into(versionedDir()) }
    project.copy { from('go-server/release/start-server.bat') into(versionedDir()) }
    project.copy { from('go-server/release/stop-server.bat') into(versionedDir()) }
    project.copy { from('go-server/release/config.properties') into(versionedDir()) }
    project.copy { from('go-server/win/cruisewrapper.exe') into(versionedDir()) }

    project.copy { from('go-server/win/config') into("${versionedDir()}/config") }
    project.copy { from('go-server/win/lib') into("${versionedDir()}/lib") }
  }
}

assemble.dependsOn(":installers:agentWindowsExe", ":installers:serverWindowsExe")
