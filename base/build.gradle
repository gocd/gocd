/*
 * Copyright 2020 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

description = 'GoCD Base'

dependencies {
  providedAtPackageTime group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: project.versions.bouncyCastle
  providedAtPackageTime group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: project.versions.bouncyCastle

  api(group: 'org.apache.ant', name: 'ant', version: project.versions.apacheAnt)  {
    transitive = false
  }
  api group: 'commons-io', name: 'commons-io', version: project.versions.commonsIO
  api group: 'org.apache.commons', name: 'commons-lang3', version: project.versions.commonsLang3
  api group: 'commons-codec', name: 'commons-codec', version: project.versions.commonsCodec
  api(group: 'org.apache.httpcomponents', name: 'httpclient', version: project.versions.apacheHttpComponents) {
    exclude(module: 'commons-codec')
    exclude(module: 'commons-logging')
  }
  api group: 'joda-time', name: 'joda-time', version: project.versions.jodaTime
  api group: 'org.slf4j', name: 'jcl-over-slf4j', version: project.versions.slf4j
  api group: 'ch.qos.logback', name: 'logback-classic', version: project.versions.logback
  testCompileOnly group: 'junit', name: 'junit', version: project.versions.junit
  testRuntimeOnly group: 'org.junit.vintage', name: 'junit-vintage-engine', version: project.versions.junit5
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: project.versions.junit5
  testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: project.versions.junit5
  testImplementation group: 'com.googlecode', name: 'junit-ext', version: '1.0'
  testImplementation group: 'org.hamcrest', name: 'hamcrest-core', version: project.versions.hamcrest
  testImplementation group: 'org.hamcrest', name: 'hamcrest-library', version: project.versions.hamcrest
  testImplementation group: 'org.mockito', name: 'mockito-core', version: project.versions.mockito
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-migrationsupport', version: project.versions.junit5
  testImplementation group: 'org.assertj', name: 'assertj-core', version: project.versions.assertJ
}

def generatedResourcesOutput = file("resources-generated")
def generatedResourcesInput = file('resource-templates')

sourceSets {
  main {
    resources {
      output.dir generatedResourcesOutput, builtBy: 'generateResources'
      srcDirs += generatedResourcesOutput
    }
  }
}

clean {
  delete(generatedResourcesOutput)
}

task generateResources {
  inputs.dir(generatedResourcesInput)
  outputs.dir(generatedResourcesOutput)

  def props = [
    'goVersion': rootProject.goVersion,
    'distVersion': rootProject.distVersion,
    'fullVersion': rootProject.fullVersion,
    'gitRevision': rootProject.gitRevision,
    'copyrightYear': rootProject.copyrightYear,
  ]

  inputs.properties(props)

  doFirst {
    project.delete(generatedResourcesOutput)
    generatedResourcesOutput.mkdirs()

    project.copy {
      into generatedResourcesOutput
      from(generatedResourcesInput) {
        expand(props)
      }
    }
  }
}

task prepare {
  dependsOn generateResources
}
