/*
 * Copyright Thoughtworks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


import com.thoughtworks.go.build.*
import groovy.text.SimpleTemplateEngine
import groovy.text.Template
import org.apache.tools.ant.types.Commandline
import org.gradle.internal.jvm.Jvm

tasks.register('pathingJar', Jar) {
  archiveClassifier = 'pathing'

  def railsClasspath = project.railsClasspath
  dependsOn(railsClasspath, configurations.compileClasspath)

  def jrubyJarLoc = project.jrubyJar
  doFirst {
    manifest {
      attributes 'Class-Path': (railsClasspath.files - [jrubyJarLoc.get()]).collect { it.toURI() }.join(' ')
    }
  }
}

tasks.register('generateJSRoutes', ExecuteUnderRailsTask) {
  outputs.cacheIf { true }
  def outputDir = file("${project.railsRoot}/webpack/gen")
  inputs.dir(file("${project.railsRoot}/vendor"))
  inputs.files(fileTree(dir: "${project.railsRoot}/config", includes: ['**/*.rb']))
  outputs.dir(outputDir)

  environment(
    'RAILS_ENV': 'production',
    'RAILS_GROUPS': 'assets',
    'OUTPUT_DIR': outputDir,
  )

  args = ['-S', 'rake', '--trace', 'generated_js']

  doFirst {
    delete outputDir
  }
}

tasks.register('createJRubyBinstubs') {
  outputs.cacheIf { true }
  dependsOn pathingJar, configurations.compileClasspath

  def outputDir = file("scripts")
  inputs.dir("script-templates")
  inputs.properties([
    bundledGemDir: project.bundledGemDir,
    railsRoot    : project.railsRoot
  ])

  def jrubyJarLoc = project.jrubyJar

  def templateProperties = [
    mainClassName       : 'org.jruby.Main',
    jvmArgs             : project.jrubyOptimizationJvmArgs,
    systemProperties    : (project.jrubyOptimizationSystemProperties + project.jrubyDefaultSystemProperties + project.railsSystemProperties),
    classpath           : [pathingJar.archiveFile.get().asFile],
    environment         : project.defaultJRubyEnvironment,
    additionalJRubyPaths: project.additionalJRubyPaths,
    javaExecutable      : Jvm.current().getExecutable("java")
  ]

  inputs.properties(templateProperties)
  outputs.dir(outputDir)

  doFirst {
    templateProperties['jrubyJar'] = jrubyJarLoc.get()
    delete(outputDir)
    mkdir(outputDir)

    ['rspec'].each { file ->
      copy {
        from "script-templates/${file}"
        into outputDir
        filePermissions {
          unix(0755)
        }
      }
    }

    ['jruby', 'jruby.bat'].each { eachFile ->
      def inputTemplate = file("script-templates/${eachFile}")
      def engine = new SimpleTemplateEngine()
      Template template = engine.createTemplate(inputTemplate)
      String output = template.make(templateProperties).toString()

      file("${outputDir}/${inputTemplate.name}").write(output)
      file("${outputDir}/${inputTemplate.name}").setExecutable(true)
    }
  }
}

tasks.register('initializeRailsGems') {
  outputs.cacheIf { true }
  dependsOn createJRubyBinstubs

  def root = project.railsRoot
  inputs.file("${root}/Gemfile")
  inputs.file("${root}/Gemfile.lock")
  inputs.file("${root}/.bundle/config")
  inputs.properties([
    jrubyVersion : project.deps.jruby,
    bundledGemDir: project.bundledGemDir,
    railsRoot    : root
  ])

  outputs.dir(project.bundledGemDir)

  def injected = project.objects.newInstance(InjectedExecOps)
  doFirst {
    JRuby.exec(project, injected.execOps) {
      workingDir = root
      args = ['-S', 'bundle', 'install']
      maxHeapSize = '1g'
      environment += [ 'JARS_SKIP': 'true' ] // Avoid issues jar-dependencies and running via jruby-complete https://github.com/jruby/jruby/issues/8886
    }
  }
}

tasks.register('yarnInstall', YarnInstallTask) {
  workingDir = project.railsRoot
}

prepare.dependsOn(initializeRailsGems, createJRubyBinstubs, yarnInstall)

tasks.register('cleanRails', Delete) {
  delete layout.buildDirectory.dir("railsTests")
  delete "${project.railsRoot}/config/cipher"
  delete "${project.railsRoot}/config/cipher.aes"
  delete "${project.railsRoot}/db/config.git"
  delete "${project.railsRoot}/db/h2db"
  delete "${project.railsRoot}/logs"
  delete "${project.railsRoot}/node_modules"
  delete "${project.railsRoot}/public/assets"
  delete "${project.railsRoot}/tmp"
  delete "${project.railsRoot}/webpack/gen"
  delete "${project.railsRoot}/yarn-error.log"
  delete "${project.railsRoot}/yarn-license-report"

  // these are purposely added as `doFirst` to avoid intellij from excluding these from the modules
  def jrubyScriptsDir = project.jrubyScriptsDir
  def bundledGemDir = project.bundledGemDir
  doFirst {
    delete jrubyScriptsDir
    delete bundledGemDir
  }
}
clean.dependsOn cleanRails

tasks.register('compileAssetsWebpackDev', YarnRunTask) {
  dependsOn yarnInstall, generateJSRoutes

  ext.licenseReportFile = layout.buildDirectory.file("reports/yarn-license/license-report.json").get().asFile
  outputs.dir(ext.licenseReportFile.parentFile)

  source(project.file("${project.railsRoot}/.babelrc"))
  source(project.file("${project.railsRoot}/tsconfig.json"))
  source(project.file("${project.railsRoot}/webpack"))
  source(project.file("${project.railsRoot}/vendor/assets"))
  source(project.file("${project.railsRoot}/config"))

  workingDir = project.railsRoot
  destinationDir = project.file("${project.railsRoot}/public/assets/webpack")
  yarnCommand = ['webpack-dev', '--env', "outputDir=${destinationDir.toPath().toString()}", '--env', "licenseReportFile=${ext.licenseReportFile}"]

  doFirst {
    delete "${project.railsRoot}/tmp" // Clean the cacheDir from webpack/config/webpack.config.ts
  }
}

tasks.register('compileAssetsWebpackProd', YarnRunTask) {
  dependsOn yarnInstall, generateJSRoutes

  ext.licenseReportFile = layout.buildDirectory.file("reports/yarn-license/license-report.json").get().asFile
  outputs.dir(ext.licenseReportFile.parentFile)

  source(project.file("${project.railsRoot}/.babelrc"))
  source(project.file("${project.railsRoot}/tsconfig.json"))
  source(project.file("${project.railsRoot}/webpack"))
  source(project.file("${project.railsRoot}/vendor/assets"))
  source(project.file("${project.railsRoot}/config"))

  workingDir = project.railsRoot
  destinationDir = layout.buildDirectory.dir("webpack-assets").get().asFile
  yarnCommand = ['webpack-prod', '--env', "outputDir=${destinationDir.toPath().toString()}", '--env', "licenseReportFile=${ext.licenseReportFile}"]

  doFirst {
    delete "${workingDir}/tmp" // Clean the cacheDir from webpack/config/webpack.config.ts
  }
}

tasks.register('compileAssetsRailsTest', ExecuteUnderRailsTask) {
  dependsOn yarnInstall
  outputs.cacheIf { true }

  def publicAssetsDir = file("${project.railsRoot}/public/assets")

  inputs.dir(file("${project.railsRoot}/config"))
  inputs.dir(file("${project.railsRoot}/app/assets"))
  inputs.dir(file("${project.railsRoot}/vendor/assets"))

  outputs.dir(publicAssetsDir)

  environment += [
    'RAILS_ENV'   : 'test',
    'RAILS_GROUPS': 'assets',
  ]

  // Native-subprocess control is needed for usage of Dart Sass via sass-embedded
  jvmArgs += [
    '--add-opens=java.base/sun.nio.ch=ALL-UNNAMED',
    '--add-opens=java.base/java.io=ALL-UNNAMED'
  ]

  args = ['-S', 'rake', '--trace', 'assets:clobber', 'assets:precompile']

  doFirst {
    delete "${workingDir}/tmp"
    delete publicAssetsDir
    publicAssetsDir.parentFile.mkdirs()
  }
}

tasks.register('compileAssetsRailsProd', ExecuteUnderRailsTask) {
  dependsOn yarnInstall
  outputs.cacheIf { true }

  def outputAssetsDir = layout.buildDirectory.dir("rails-assets")
  def publicAssetsDir = layout.buildDirectory.dir("public/assets")

  inputs.dir(file("${project.railsRoot}/config"))
  inputs.dir(file("${project.railsRoot}/app/assets"))
  inputs.dir(file("${project.railsRoot}/vendor/assets"))

  outputs.dir(outputAssetsDir)

  disableJRubyOptimization = true

  environment(
    'RAILS_ENV': 'production',
    'RAILS_GROUPS': 'assets',
  )

  // Native-subprocess control is needed for usage of Dart Sass via sass-embedded
  jvmArgs += [
    '--add-opens=java.base/sun.nio.ch=ALL-UNNAMED',
    '--add-opens=java.base/java.io=ALL-UNNAMED'
  ]

  args = ['-S', 'rake', '--trace', 'assets:clobber', 'assets:precompile']

  doFirst {
    delete "${workingDir}/tmp"
    delete outputAssetsDir
    outputAssetsDir.get().asFile.parentFile.mkdirs()
  }

  doLast {
    copy {
      from publicAssetsDir
      into outputAssetsDir
    }

    delete publicAssetsDir
  }
}

tasks.register('compileAssets') {
  dependsOn compileAssetsWebpackProd
  dependsOn licenseReportAggregate
  dependsOn compileAssetsRailsProd
  inputs.files licenseReportAggregate.outputs.files
  inputs.files compileAssetsRailsProd.outputs.files
  inputs.files compileAssetsWebpackProd.outputs.files

  def outputDir = layout.buildDirectory.dir("assets").get().asFile
  outputs.dir(outputDir)

  doFirst {
    delete outputDir

    copy {
      from licenseReportAggregate
      // assets are cached with a long lived cache header
      // we purposefully add a version string in the path so every new build will basically use a different url
      into "${outputDir}/dependency-license-report-${version}"
    }
    copy {
      from compileAssetsRailsProd
      into outputDir
    }
    copy {
      from compileAssetsWebpackProd
      into "${outputDir}/webpack"
    }
  }
}

tasks.register('rspec', ExecuteUnderRailsTask) {
  dependsOn yarnInstall
  mustRunAfter generateJSRoutes, compileAssets, compileAssetsWebpackDev, compileAssetsRailsTest
  outputs.cacheIf { true }
  inputs.files project.railsRoot
  inputs.files project.railsClasspath

  description = "Run specs (pass '-Popts=...' to pass options)"

  disableJRubyOptimization = true

  environment += [
    'REPORTS_DIR': "${layout.buildDirectory.get()}/rspec-results"
  ]

  def heapDumps = "${layout.buildDirectory.get()}/heap-dumps"
  jvmArgs += InstallerType.server.jvmModuleOpensArgs + ['-XX:+HeapDumpOnOutOfMemoryError', "-XX:HeapDumpPath=${heapDumps}"]
  args = ['-S', 'rspec', '--backtrace']
  maxHeapSize = '2g'

  if (project.hasProperty('opts')) {
    args += Commandline.translateCommandline(project.property('opts') as String)
  }


  doFirst {
    mkdir(heapDumps)
    file("${heapDumps}/.keep").setText("")
  }
}

tasks.register('railsExec', ExecuteUnderRailsTask) {
  outputs.upToDateWhen { false }

  description = "rails exec (pass '-Pcmd=...' to pass options)"

  args = Commandline.translateCommandline(project.properties.get('cmd') as String)
}
